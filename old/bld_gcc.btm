@echo off
setlocal

@echo.
@echo --- CONFIGURATION ---

set OBJ_DIR=obj
set SRC_FILES=presto\kernel.c presto\task_sw.asm presto\clock.c presto\system.c services\debugger.c services\lcd.c services\i2c.c services\motors.c services\serial.c services\sound.c app\test.c
set TARGET=hbtest

PATH=%PATH%;c:\alan\play\hc11\gcc\bin

@echo.
@echo --- CLEANING UP ---

del /s/q *.bak *.aaa >&> NUL:
del /s/q *.s *.i >&> NUL:
rem del /s/y/x/q %OBJ_DIR% >&> NUL:
rem del /s/q *.s19 >&> NUL:

@echo.
@echo --- PREPARING ---

mkdir %OBJ_DIR% >&> NUL:

@echo.
@echo --- COMPILING AND ASSEMBLING ---

set ICC11_INCLUDE=H:\ICC\INCLUDE
@rem -c for compile to object code
@rem -l for interspersed C/asm listings
@rem -e for C++ comments

set counter=0
do
   set file=%@WORD[%counter,%SRC_FILES]
   set counter=%@INC[%counter]
   if "%file%" == "" leave

   echos FILE %file%...
   set obj=%OBJ_DIR%\%@NAME[%file%].o
   set work=0
   if NOT EXIST %obj% set work=1
   iff %work% == 0 then
      if %@FILEAGE[%file%] GT %@FILEAGE[%obj%] set work=1
   endiff
   iff %work% == 1 then
      set ext=%@LOWER[%@EXT[%file%]]
      iff "%ext%"=="c" then
         @echo COMPILING
         xgcc -c -I. -I%ICC11_INCLUDE% -o %obj% %file%
         if errorlevel 1 goto abort
      elseiff "%ext%"=="asm" then
         @echo ASSEMBLING
         as6811 -o %obj% %file%
         if errorlevel 1 goto abort
         move /q %@PATH[%file%]\%@NAME[%file%].lis %OBJ_DIR%
      else
         @echo HUH?
      endiff
   else
      @echo OK
   endiff
   set OBJS=%OBJS% %obj%
enddo


@echo.
@echo --- LINKING ---

@rem -bdata:0x0000.0x1111:0x2222.0x3333 for multiple zones
@rem define heap_size for crt11.o to allocate heap area
@rem define init_sp for your initial stack pointer
@rem -m option to produce map file

set ICC11_LIB=H:\ICC\LIB
set ICC11_LINKER_OPTS=-bdata:0x8000.0x9fff -bbss:0xa000.0xb5ff -btext:0xc000.0xffbf -m -dheap_size:0x800 -dinit_sp:0xbfbf

@rem   ADDRESSES    SIZE   USED FOR WHAT
@rem ----------------------------------------------------
@rem   8000-9FFF      8k   DATA - GLOBAL VARIABLES
@rem   A000-B5FF    5632   BSS - INITIALIZED GLOBAL VARIABLES
@rem   B600-B7FF     512   EEPROM
@rem   B800-BFBF   2k-64   INITIAL STACK
@rem   BFC0-BFFF      64   SPECIAL INTERRUPTS
@rem   C000-FFBF  16k-64   TEXT - CODE INSTRUCTIONS, CONSTANT DATA
@rem   FFC0-FFFF      64   NORMAL INTERRUPTS
@rem ----------------------------------------------------

echo -L%ICC11_LIB% -o %TARGET%.S19 %OBJS% > BUILD.TMP
icc11 @BUILD.TMP
del /q BUILD.TMP

@echo.
@echo --- SHOW THE RESULTS ---

dir /k/m *.s19

:abort

@echo.
@echo --- CLEAN UP ---

del /s/q *.bak *.aaa >&> NUL:
del /s/q *.s *.i >&> NUL:
move /q *.lis *.lst *.mp %OBJ_DIR% >&> NUL:

endlocal

